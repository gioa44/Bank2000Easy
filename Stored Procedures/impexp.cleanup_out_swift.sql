SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [impexp].[cleanup_out_swift] (@date smalldatetime)  AS
BEGIN
	BEGIN TRAN

	DELETE FROM impexp.PORTIONS_OUT_SWIFT
	WHERE PORTION_DATE <= @date AND ([COUNT] = 0 AND AMOUNT = $0)
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END
	
	DECLARE @tbl TABLE (PORTION_DATE smalldatetime NOT NULL, PORTION int NOT NULL, PRIMARY KEY (PORTION_DATE, PORTION))
	DECLARE @tbl2 TABLE (DOC_REC_ID int NOT NULL PRIMARY KEY)

	INSERT INTO impexp.PORTIONS_OUT_SWIFT_ARC
	OUTPUT inserted.PORTION_DATE, inserted.PORTION into @tbl
	SELECT A.*
	FROM impexp.PORTIONS_OUT_SWIFT A
	WHERE A.PORTION_DATE <= @date AND A.STATE = 4 AND 
		NOT EXISTS(SELECT * FROM impexp.DOCS_OUT_SWIFT B WHERE B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION AND (B.STATE <> 9 OR B.DOC_DATE > @date))
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END
	
	INSERT INTO impexp.DOCS_OUT_SWIFT_ARC
	OUTPUT inserted.DOC_REC_ID into @tbl2
	SELECT A.*
	FROM impexp.DOCS_OUT_SWIFT A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	INSERT INTO impexp.DOCS_OUT_SWIFT_ARC_CHANGES
	SELECT A.*
	FROM impexp.DOCS_OUT_SWIFT_CHANGES A
		INNER JOIN @tbl2 B ON B.DOC_REC_ID = A.DOC_REC_ID
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.DOCS_OUT_SWIFT_CHANGES A
		INNER JOIN @tbl2 B ON B.DOC_REC_ID = A.DOC_REC_ID
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.DOCS_OUT_SWIFT A
		INNER JOIN @tbl2 B ON B.DOC_REC_ID = A.DOC_REC_ID
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.PORTIONS_OUT_SWIFT A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	COMMIT
	RETURN @@ERROR
END
GO
