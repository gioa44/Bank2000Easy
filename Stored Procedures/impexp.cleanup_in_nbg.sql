SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [impexp].[cleanup_in_nbg] (@date smalldatetime)  AS
BEGIN
	BEGIN TRAN

	DELETE FROM impexp.PORTIONS_IN_NBG
	WHERE PORTION_DATE <= @date AND ([COUNT] = 0 AND AMOUNT = $0)
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END
	
	DECLARE @tbl TABLE (PORTION_DATE smalldatetime NOT NULL, PORTION int NOT NULL, PRIMARY KEY (PORTION_DATE, PORTION ))

	INSERT INTO impexp.PORTIONS_IN_NBG_ARC
	OUTPUT inserted.PORTION_DATE, inserted.PORTION into @tbl
	SELECT A.*
	FROM impexp.PORTIONS_IN_NBG A
	WHERE A.PORTION_DATE <= @date AND A.STATE = 4 AND 
		NOT EXISTS(SELECT * FROM impexp.DOCS_IN_NBG B WHERE B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION AND (B.STATE NOT IN (4, 99) OR B.DOC_DATE > @date))
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END
	
	INSERT INTO impexp.DOCS_IN_NBG_ARC
	SELECT A.*
	FROM impexp.DOCS_IN_NBG A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	INSERT INTO impexp.DOCS_IN_NBG_ARC_CHANGES
	SELECT A.*
	FROM impexp.DOCS_IN_NBG_CHANGES A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.DOCS_IN_NBG_CHANGES A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.DOCS_IN_NBG A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	DELETE A
	FROM impexp.PORTIONS_IN_NBG A
		INNER JOIN @tbl B ON B.PORTION_DATE = A.PORTION_DATE AND B.PORTION = A.PORTION
	IF @@ERROR <> 0 BEGIN IF @@TRANCOUNT > 0 ROLLBACK RETURN 1 END

	COMMIT
	RETURN @@ERROR
END
GO
