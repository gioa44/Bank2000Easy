SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO


CREATE PROCEDURE [dbo].[loan_rep_overdued_loans] (
	@user_id int,
	@right_name varchar(100) = 'ÍÀáÅÀ',
	@field_list varchar(1000) = NULL,
	@view_name sysname = 'dbo.LOAN_VW_LOAN_REPS',
	@where_sql1 varchar(1000) = NULL,
	@where_sql2 varchar(1000) = NULL,
	@where_sql3 varchar(1000) = NULL,
	@join_sql varchar(1000) = NULL,
	@date smalldatetime,
	@distinct bit = 0)
AS
SET NOCOUNT ON;

CREATE TABLE #tbl (LOAN_ID INT PRIMARY KEY)

INSERT INTO #tbl
EXEC [dbo].[loan_show_loans]
	@user_id = @user_id,
	@right_name = @right_name,
	@field_list = 'L.LOAN_ID',
	@view_name = 'dbo.LOANS',
	@where_sql1 = @where_sql1,
	@where_sql2 = @where_sql2,
	@where_sql3 = @where_sql3,
	@join_sql = @join_sql,
	@distinct = @distinct

---ÃÒÏÄÁÉÈÉ Table
CREATE TABLE #tbl1 (LOAN_ID INT NOT NULL, 
					CALC_DATE smalldatetime NULL, 
					NU_PRINCIPAL money NULL,
					NU_INTEREST money NULL,
					PRINCIPAL money NULL,
					INTEREST money NULL,
					OVERDUE_DATE smalldatetime NULL,
					OVERDUE_PRINCIPAL money NULL,
					OVERDUE_PRINCIPAL_INTEREST money NULL,
					OVERDUE_PRINCIPAL_PENALTY money NULL,				
					OVERDUE_PERCENT money NULL,
					OVERDUE_PERCENT_PENALTY money NULL,
					OVERDUE_DAYS int NULL)

---ÊÖÒÓÏÒÉ
DECLARE cc CURSOR LOCAL FAST_FORWARD
FOR
SELECT LOAN_ID
FROM #tbl

DECLARE @loan_id int
OPEN cc
FETCH NEXT FROM cc INTO @loan_id

WHILE @@FETCH_STATUS = 0
BEGIN
	INSERT INTO #tbl1
	(LOAN_ID,
	CALC_DATE, 
	NU_PRINCIPAL,
	NU_INTEREST,
	PRINCIPAL,
	INTEREST,
	OVERDUE_DATE,
	OVERDUE_PRINCIPAL,
	OVERDUE_PRINCIPAL_INTEREST,
	OVERDUE_PRINCIPAL_PENALTY,
	OVERDUE_PERCENT,
	OVERDUE_PERCENT_PENALTY,
	OVERDUE_DAYS)
	SELECT LDD.LOAN_ID, LDD.CALC_DATE,
		LDD.NU_PRINCIPAL, 
		LDD.NU_INTEREST, 
		LDD.PRINCIPAL, 
		LDD.INTEREST,
		LDD.OVERDUE_DATE,
		LDD.OVERDUE_PRINCIPAL,
		LDD.OVERDUE_PRINCIPAL_INTEREST,
		LDD.OVERDUE_PRINCIPAL_PENALTY,
		LDD.OVERDUE_PERCENT,
		LDD.OVERDUE_PERCENT_PENALTY,
		DATEDIFF(day, OVERDUE_DATE, CALC_DATE)
	FROM dbo.loan_get_loan_detail_data(@loan_id, @date) LDD
	WHERE LDD.OVERDUE_DATE IS NOT NULL

	FETCH NEXT FROM cc INTO @loan_id
END
CLOSE cc
DEALLOCATE cc

DECLARE @sql nvarchar(4000)

IF @field_list IS NULL OR @field_list = '*'
	SET @field_list = 'L.*'

SET @sql = N'
SELECT ' + @field_list + N', R.OVERDUE_DATE, R.OVERDUE_DAYS,
	R.NU_PRINCIPAL, 
	(R.NU_INTEREST + R.INTEREST + OVERDUE_PRINCIPAL_INTEREST) AS INTEREST, 
	R.PRINCIPAL,
	R.OVERDUE_PERCENT, 
	R.OVERDUE_PRINCIPAL_PENALTY + R.OVERDUE_PERCENT_PENALTY AS PENALTY,
	R.OVERDUE_PRINCIPAL, 
	R.OVERDUE_PERCENT + (R.OVERDUE_PRINCIPAL_PENALTY + R.OVERDUE_PERCENT_PENALTY) + R.OVERDUE_PRINCIPAL AS SCHEDULE_AMOUNT,
	R.INTEREST + R.OVERDUE_PERCENT + (R.OVERDUE_PRINCIPAL_PENALTY + R.OVERDUE_PERCENT_PENALTY) AS INTEREST_AMOUNT,
	PRINCIPAL + OVERDUE_PRINCIPAL AS PRINCIPAL_AMOUNT,	
	R.INTEREST + R.OVERDUE_PERCENT + (R.OVERDUE_PRINCIPAL_PENALTY + R.OVERDUE_PERCENT_PENALTY) + PRINCIPAL + OVERDUE_PRINCIPAL AS TOTAL_AMOUNT

FROM ' + @view_name + N' L
	INNER JOIN #tbl1 R ON R.LOAN_ID = L.LOAN_ID'

EXEC sp_executesql @sql, N'@date smalldatetime', @date

DROP TABLE #tbl
DROP TABLE #tbl1

GO
