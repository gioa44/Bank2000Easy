SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO


CREATE VIEW [export].[OVERDUED_LOANS]
AS
SELECT
		export.get_client_type3(c.CLIENT_NO) AS CLIENT_TYPE,
		l.LOAN_ID,
		c.CLIENT_NO AS CLIENT_ID,
		export.get_bank_un_number() AS BANK_UN_NUMBER,
		l.AGREEMENT_NO AS AGREEMENT_NO, 
		CASE WHEN EXISTS(SELECT * FROM export.LOAN_STATE el WHERE el.LOAN_ID = l.LOAN_ID) THEN 'C' ELSE 'N' END AS NEW_CHANGE,
		export.get_client_code(c.CLIENT_NO) AS CLAIM_SUBJECT,
		export.get_bank_un_number() AS CLAIM_OWNER,
		ld.MAX_CATEGORY_LEVEL AS CONTRACT_STATUS,
		l.ISO AS CCY,
		1 AS RECORD_STATUS,
		NULL AS CASE_DATE,
		ISNULL(ld.OVERDUE_DATE, CAST('19000101' AS smalldatetime)) AS DEFAULT_DATE, 
		lt.CODE,
		l.AMOUNT,
		ISNULL(ld.PRINCIPAL, $0.00) + ISNULL(ld.OVERDUE_PRINCIPAL, $0.00) + ISNULL(ld.WRITEOFF_PRINCIPAL, $0.00) AS CURRENT_BALANCE, 
		ISNULL(ld.OVERDUE_PRINCIPAL, $0.00) + ISNULL(ld.WRITEOFF_PRINCIPAL, $0.00) AS OVERDUE_PRINCIPAL_AMOUNT,
		ISNULL(ld.INTEREST, $0.00) + ISNULL(ld.OVERDUE_PERCENT, $0.00) + ISNULL(ld.WRITEOFF_PERCENT, $0.00) AS ACCRUID_INTEREST,
		ISNULL(ld.OVERDUE_PRINCIPAL_PENALTY, $0.00) + ISNULL(ld.OVERDUE_PERCENT_PENALTY, $0.00) + ISNULL(ld.WRITEOFF_PENALTY, $0.00) AS ACCRUID_PENALTIES, 
		NULL AS PAYED_UP_DATE 
	FROM dbo.LOANS l
 		 INNER JOIN dbo.CLIENTS c ON l.CLIENT_NO = c.CLIENT_NO
		 LEFT JOIN dbo.LOAN_DETAILS ld ON l.LOAN_ID = ld.LOAN_ID
		 INNER JOIN dbo.LOAN_TYPES lt ON l.LOAN_TYPE = lt.[TYPE_ID]
	WHERE l.STATE IN (dbo.loan_const_state_overdued(), dbo.loan_const_state_writedoff()) AND 

  		dbo.get_equ(ISNULL(ld.OVERDUE_PRINCIPAL, $0.00) + ISNULL(ld.WRITEOFF_PRINCIPAL, $0.00) + ISNULL(ld.OVERDUE_PERCENT, $0.00) +
		ISNULL(ld.WRITEOFF_PERCENT, $0.00) + ISNULL(ld.OVERDUE_PRINCIPAL_PENALTY, $0.00) + ISNULL(ld.OVERDUE_PERCENT_PENALTY, $0.00) + 
		ISNULL(ld.WRITEOFF_PENALTY, $0.00), l.ISO, dbo.loan_open_date()) > $10.00 AND

		(  ( (c.CLIENT_TYPE = 1) AND (DATEDIFF(DAY, ISNULL(ld.OVERDUE_DATE, 0), dbo.loan_open_date()) > 30) ) OR
		(DATEDIFF(DAY, ISNULL(ld.OVERDUE_DATE, 0), dbo.loan_open_date()) > 60)  ) AND (GUARANTEE = 0)

	UNION

	SELECT
		export.get_client_type3(c.CLIENT_NO) AS CLIENT_TYPE,
		l.LOAN_ID,
		c.CLIENT_NO AS CLIENT_ID,
		export.get_bank_un_number() AS BANK_UN_NUMBER,
		l.AGREEMENT_NO AS AGREEMENT_NO, 
		'C' AS NEW_CHANGE,
		export.get_client_code(c.CLIENT_NO) AS CLAIM_SUBJECT,
		export.get_bank_un_number() AS CLAIM_OWNER,
		CASE WHEN l.STATE = dbo.loan_const_state_closed() THEN 0 ELSE ld.MAX_CATEGORY_LEVEL END AS CONTRACT_STATUS,
		l.ISO AS CCY,
		1 AS RECORD_STATUS,
		NULL AS CASE_DATE,
		ISNULL(ld.OVERDUE_DATE, CAST('19000101' AS smalldatetime)) AS DEFAULT_DATE, 
		lt.CODE,
		l.AMOUNT,
		ISNULL(ld.PRINCIPAL, $0.00) + ISNULL(ld.OVERDUE_PRINCIPAL, $0.00) + ISNULL(ld.WRITEOFF_PRINCIPAL, $0.00) AS CURRENT_BALANCE, 
		ISNULL(ld.OVERDUE_PRINCIPAL, $0.00) + ISNULL(ld.WRITEOFF_PRINCIPAL, $0.00) AS OVERDUE_PRINCIPAL_AMOUNT,
		ISNULL(ld.INTEREST, $0.00) + ISNULL(ld.OVERDUE_PERCENT, $0.00) + ISNULL(ld.WRITEOFF_PERCENT, $0.00) AS ACCRUID_INTEREST,
		ISNULL(ld.OVERDUE_PRINCIPAL_PENALTY, $0.00) + ISNULL(ld.OVERDUE_PERCENT_PENALTY, $0.00) + ISNULL(ld.WRITEOFF_PENALTY, $0.00) AS ACCRUID_PENALTIES, 
		export.get_close_date(l.LOAN_ID) AS PAYED_UP_DATE 
	FROM dbo.LOANS l
		 INNER JOIN export.LOAN_STATE ls ON ls.LOAN_ID = l.LOAN_ID
 		 INNER JOIN dbo.CLIENTS c ON l.CLIENT_NO = c.CLIENT_NO
		 LEFT JOIN dbo.LOAN_DETAILS ld ON l.LOAN_ID = ld.LOAN_ID
		 INNER JOIN dbo.LOAN_TYPES lt ON l.LOAN_TYPE = lt.[TYPE_ID]
	WHERE ls.SEND_STATE = 0
GO
