SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE FUNCTION [easyrep].[loans_get_working_schedule](@loan_id int, @date smalldatetime)
RETURNS @tbl TABLE (LoanId int NOT NULL, ScheduleDate smalldatetime, 
	Amount money, Principal money, Interest Money, Balance Money)
AS
BEGIN
DECLARE
	@op_id int;

	WITH scheds AS 
	(SELECT LOAN_ID, OP_ID 
		FROM dbo.LOAN_SCHEDULE_HISTORY (NOLOCK) 
		WHERE LOAN_ID = @loan_id
		GROUP BY LOAN_ID, OP_ID
	)
	SELECT TOP 1 @op_id = ops.OP_ID
	FROM scheds	
		INNER JOIN dbo.LOAN_OPS (NOLOCK) ops ON scheds.OP_ID = ops.OP_ID 
	WHERE ops.OP_DATE > @date
	ORDER BY ops.OP_DATE ASC
	
	IF @op_id IS NULL
	BEGIN
		INSERT INTO @tbl
		SELECT 
			LOAN_ID, SCHEDULE_DATE, ORIGINAL_AMOUNT, ORIGINAL_PRINCIPAL, ORIGINAL_INTEREST, ORIGINAL_BALANCE 
		FROM dbo.LOAN_SCHEDULE (NOLOCK)
		WHERE LOAN_ID = @loan_id AND ORIGINAL_AMOUNT IS NOT NULL
	END
	ELSE
	BEGIN
		INSERT INTO @tbl
		SELECT 
			LOAN_ID, SCHEDULE_DATE, ORIGINAL_AMOUNT, ORIGINAL_PRINCIPAL, ORIGINAL_INTEREST, ORIGINAL_BALANCE 
		FROM dbo.LOAN_SCHEDULE_HISTORY (NOLOCK)
		WHERE LOAN_ID = @loan_id AND OP_ID = @op_id AND ORIGINAL_AMOUNT IS NOT NULL
	END
			
	RETURN 
END
GO
